apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId archivesBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

if (project.hasProperty("bintray_user") && project.hasProperty("bintray_api_key")) {
    bintray {
        // define this in ~/.gradle/gradle.properties
        user = bintray_user
        key = bintray_api_key

        publish = true
        publications = ['mavenJava']

        if (project.hasProperty("dryRun")) {
            dryRun = true
        }

        pkg {
            repo = 'maven'
            userOrg = 'readdle'
            name = 'swift-java-codegen'

            desc = 'Annotation processor which can generate swift bridging code by Java annotation based mappings'
            vcsUrl = 'https://github.com/andriydruk/swift-java-codegen'
            githubRepo = 'andriydruk/swift-java-codegen'

            licenses = ['Apache-2.0']

            labels = ['swift', 'java', 'android']
            publicDownloadNumbers = true

            attributes = ['maturity': 'Experimental']
        }
    }
}